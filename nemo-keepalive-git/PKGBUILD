## $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=nemo-keepalive-git
pkgver=1.8.6.r0.g18e7ed3
pkgrel=1
pkgdesc="CPU and display keepalive and scheduling library"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/nemo-keepalive"
license=('LGPL-2.1-or-later')
depends=('qt5-declarative' 'libiphb' 'mce-headers-git')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/${pkgname%-git}"
    qmake-qt5
    make
#    make -C lib-glib
#    make -C tools
}

package() {
    cd "$srcdir/${pkgname%-git}"
    mkdir -p $pkgdir/usr/lib/pkgconfig/
    make -j 1 INSTALL_ROOT="$pkgdir/" install
#    make -C lib-glib install ROOT="$pkgdir/"
#    make -C tools install ROOT="$pkgdir/"

    # Remove tests
    rm -rf "$pkgdir"/opt

}
