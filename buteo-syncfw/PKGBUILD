# $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=buteo-syncfw-git
pkgver=0.10.11.r0.g52d95ec

pkgrel=1
pkgdesc="Synchronization backend"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/buteo-syncfw"
license=('LGPLv2')
depends=('libiphb-git' 'nemo-keepalive-git' 'qt5-mce-git' 'signond' 'libaccounts-qt')
makedepends=('git' 'qt5-tools' 'doxygen')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "msyncd.service")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
}

build() {
  cd "${srcdir}/${pkgname}"
  qmake-qt5 CONFIG+=usb-moded DEFINES+=USE_KEEPALIVE
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install

  rm "${pkgdir}/usr/lib/systemd/user/msyncd.service"
  cp ${srcdir}/msyncd.service "${pkgdir}/usr/lib/systemd/user/msyncd.service"

  mkdir -p ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants/
  ln -s ../msyncd.service ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants/

  # Remove installed tests
  rm -r "$pkgdir"/opt
}
