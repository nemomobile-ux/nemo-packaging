## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=dsme-git
pkgver=0.83.2.r0.g24c6e56
pkgrel=1
pkgdesc="Device State Management Entity"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/dsme"
license=('GPL')
depends=('libngf-git' 'libiphb-git' 'mce-git' 'cryptsetup' 'libsystemd' 'libdsme>=0.66.6' 'glib2' 'systemd' 'systemd-libs')
makedepends=('git' 'autoconf' 'automake' 'mce-headers')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}" 'fix-dsme_server_path.patch' 'dsme.service' '0001-DSME-Use-wheel-group-as-priveleged.patch')
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/${pkgname%-git}"
    git submodule update --init --recursive
    patch -p1 --input="${srcdir}/fix-dsme_server_path.patch"
    patch -p1 --input="${srcdir}/0001-DSME-Use-wheel-group-as-priveleged.patch"
}

build() {
    cd "$srcdir/${pkgname%-git}"
    ./autogen.sh
    ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--sbindir=/usr/bin \
	--disable-poweron-timer \
	--disable-static \
	--disable-upstart \
	--enable-runlevel \
	--enable-systemd \
	--enable-pwrkeymonitor \
	--disable-validatorlistener \
	--enable-abootsettings \
	--enable-usewheel
    make
}

package() {
    cd "$srcdir/${pkgname%-git}"
    make DESTDIR="$pkgdir/" install

    mkdir -p $pkgdir/usr/lib/systemd/system/
    cp $srcdir/dsme.service $pkgdir/usr/lib/systemd/system/
}
