## $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=mce-git
pkgver=1.109.0.r0.g7297d9e
pkgrel=1
pkgdesc="Mode Control Entity for Nokia mobile computers"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/mce.git"
license=('GPL')
depends=('libngf-git' 'libiphb-git')
makedepends=('git' 'doxygen' 'libdsme' 'libsystemd' 'usb-moded' 'mce-headers')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}" '0001-charger-Add-pinephone-charger-detection.-Fixes-JB-49.patch')
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}


prepare() {
    cd "$srcdir/${pkgname%-git}"
    sed -i "s|libsystemd-daemon|libsystemd|g" ./Makefile
    git submodule update --init --recursive
    patch -p1 --input="${srcdir}/0001-charger-Add-pinephone-charger-detection.-Fixes-JB-49.patch"
}

build() {
    cd "$srcdir/${pkgname%-git}"
    make

}

package() {
    cd "$srcdir/${pkgname%-git}"
    make DESTDIR="$pkgdir/" install

    mv $pkgdir/usr/sbin $pkgdir/usr/bin

    rm $pkgdir/lib/systemd/system/multi-user.target.wants/mce.service
    rmdir $pkgdir/lib/systemd/system/multi-user.target.wants
    mv $pkgdir/lib/systemd $pkgdir/usr/lib/systemd

    sed -i 's/Before=basic.target//' $pkgdir/usr/lib/systemd/system/mce.service

    mkdir -p $pkgdir/usr/lib/tmpfiles.d
    echo "d /var/run/mce 0755 root root -" > $pkgdir/usr/lib/tmpfiles.d/mce.conf
    rm -rf $pkgdir/lib
    rm -rf $pkgdir/var/run
}
