# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=contactsd-git

pkgver=1.4.10.r0.g5b8426b

pkgrel=1
pkgdesc="Telepathy <> QtContacts bridge for contacts"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/contactsd"
license=('BSD-3-Clause')
depends=('telepathy-qt' 'mlite' 'libmlocale' 'libqofono-qt5' 'qt5-contacts-sqlite-extensions-git' 'buteo-syncfw' 'kcalendarcore' 'mkcal-git' 'nemo-qml-plugin-contacts-git' 'qt5-ofono-nemo-extensions-git')
makedepends=('git' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "0001-KcalendarCore_not_provide_pc_files.patch" "contactsd.service" '0002-plugins-requires-QIcon-which-is-in-gui-module.patch')
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 --input="${srcdir}/0001-KcalendarCore_not_provide_pc_files.patch"
  patch -p1 --input="${srcdir}/0002-plugins-requires-QIcon-which-is-in-gui-module.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install
  rm ${pkgdir}/usr/lib/systemd/user/contactsd.service
  cp ${srcdir}/contactsd.service ${pkgdir}/usr/lib/systemd/user/contactsd.service
  mkdir -p ${pkgdir}/usr/lib/systemd/user/graphical-session-pre.target.wants
  ln -s ../contactsd.service ${pkgdir}/usr/lib/systemd/user/graphical-session-pre.target.wants
}
