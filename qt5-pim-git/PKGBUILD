# $Id$
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Ivan Semkin (ivan at semkin dot ru)
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=qt5-pim-git
pkgver=r1020.8fec622c
pkgrel=1
pkgdesc='Qt Personal Information Management'
url='http://code.qt.io/cgit/qt/qtpim.git'
arch=(x86_64 aarch64)
license=(LGPL-2.1-only LGPL-3.0-only GPL-3.0-only Qt-GPL-exception-1.0)
conflicts=(qt5-pim)
provides=(qt5-pim)
depends=(qt5-declarative)
makedepends=(git)
source=("${pkgname%-git}::git+${url}" "0001-Patch-module-version.patch")
sha512sums=('SKIP'
'6e7d43f36bbf23dedd860727259a4bf5dfad8e8548a3d4a19c974b9afed87ebb25264cc08572bdbb0f703c95ae1a5f6a7a5ca8cd742feaf46440fe7c282535d2')

prepare() {
  mkdir -p build
  cd qtpim
  git checkout 8fec622c
  patch -p1 --input="${srcdir}/0001-Patch-module-version.patch"
}

build() {
  cd build
  qmake-qt5 ../qtpim
  make
}

check() {
  cd build
  make check TESTARGS="-silent"
}

package() {
  cd build
  make -j 1 INSTALL_ROOT="$pkgdir/" install

  # Drop QMAKE_PRL_BUILD_DIR because it references the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;
}
