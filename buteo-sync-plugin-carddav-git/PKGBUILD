# $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=buteo-sync-plugin-carddav-git
pkgver=0.1.7.r0.g960aa46
pkgrel=2
pkgdesc="Syncs contact data from CardDAV services"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/buteo-sync-plugin-carddav"
license=('BSD-3-Clause')
depends=('libphonenumber' 'qt5-mlocale-git' 'buteo-syncfw-git' 'qt5-contacts-sqlite-extensions-git')
makedepends=('git' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "0001-remove-obsolete-QContact-IgnoreAccessConstraints.patch")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
#    patch -p1 --input="${srcdir}/update-api-for-qtpim-5.9.patch"
    patch -p1 --input="${srcdir}/0001-remove-obsolete-QContact-IgnoreAccessConstraints.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  # Not possible to install in build subdir
  qmake-qt5
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install
  # Remove installed tests
  rm -r "$pkgdir"/opt
}
