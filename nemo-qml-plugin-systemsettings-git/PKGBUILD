## $Id$
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev (aa13q) <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=nemo-qml-plugin-systemsettings-git
pkgver=0.5.76.r3.g296a4a9
pkgrel=1
pkgdesc="System settings plugin for Nemo Mobile"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/nemo-qml-plugin-systemsettings.git"
license=('BSD')
depends=('qt5-systems' 'qt5-usb-moded-git' 'profiled-git' 'qt5-timed-git' 'libsailfishkeyprovider-git' 'nemo-qml-plugin-models' 'qt5-connman-git' 'mlite' 'mce-git' 'nemo-qml-plugin-dbus' 'qt5-xmlpatterns')
makedepends=('git' 'qt5-tools' 'mce-headers-git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/${pkgname%-git}"
    git checkout nemomobile
}

build() {
    cd "$srcdir/${pkgname%-git}"
    qmake
    make
}

package() {
    cd "$srcdir/${pkgname%-git}"
    make -j 1 INSTALL_ROOT="$pkgdir/" install
    cd "$pkgdir"
    rm -rf opt
    mkdir -p usr/lib/${pkgname}
    mv usr/libexec/* usr/lib/${pkgname}
    rm -rf usr/libexec
}
