## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=ohm
pkgver=1.3.1.r0.g0ec3251
pkgrel=1
pkgdesc="Open Hardware Manager"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/ohm"
license=('LGPL-2.1-only')
depends=('glib2' 'libtrace-ohm' 'dbus-glib')
makedepends=('git' 'automake' 'autoconf' 'check')
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static \
    --without-xauth \
    --with-distro=meego
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/lib/systemd/system/basic.target.wants/
  ln -s ../ohmd.service $pkgdir/usr/lib/systemd/system/basic.target.wants/
  ln -s ohmd.service $pkgdir/usr/lib/systemd/system/dbus-org.freedesktop.ohm.service

  install -d ${pkgdir}/var/lib/ohm

#modules config provide by glacier-wayland-session package
  rm -rf ${pkgdir}/etc/ohm/modules.ini
}

