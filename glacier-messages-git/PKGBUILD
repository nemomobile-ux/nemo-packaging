# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_host="github.com"
_project=nemomobile-ux
_basename=glacier-messages
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=0.2.2.r28.gfab6edb

pkgrel=1
pkgdesc="QML based messages application for nemomobile"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname#branch=$_branch"
license=('BSD-3-Clause')
depends=('qt5-glacier-app-git'
	'nemo-qml-plugin-contacts-git'
	'nemo-qml-plugin-dbus'
	'commhistory-daemon-git'
	'libcommhistory-git'
	'nemo-qml-plugin-messages-git'
	'telepathy-mission-control'
	'telepathy-ofono'
	'glacier-contacts')
makedepends=('git' 'cmake' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install

    mkdir -p ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    cd ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    ln -s /usr/share/glacier-messages/glacier-messages.png icon-lock-sms.png
}
