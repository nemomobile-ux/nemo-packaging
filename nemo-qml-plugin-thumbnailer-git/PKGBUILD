# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=nemo-qml-plugin-thumbnailer-git
pkgver=1.0.6.r0.g9d9b6fa
pkgrel=1
pkgdesc="Thumbnail provider plugin for Nemo Mobile"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/nemo-qml-plugin-thumbnailer"
license=('BSD-3-Clause')
depends=('qt5-declarative')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
  cd "${srcdir}/${pkgname}"
  mkdir -p build
  cd build
  qmake ..
  make
}

package() {
  cd "${srcdir}/${pkgname}/build"
  make -j 1 INSTALL_ROOT="${pkgdir}" install

    mkdir -p ${pkgdir}/usr/lib/qt/qml/org/nemomobile/thumbnailer/
    ln -sf /usr/lib/qt/qml/Nemo/Thumbnailer/libnemothumbnailer.so ${pkgdir}/usr/lib/qt/qml/org/nemomobile/thumbnailer/
    sed 's/Nemo.Thumbnailer/org.nemomobile.thumbnailer/' < ${pkgdir}/usr/lib/qt/qml/Nemo/Thumbnailer/qmldir > ${pkgdir}/usr/lib/qt/qml/org/nemomobile/thumbnailer/qmldir

}
