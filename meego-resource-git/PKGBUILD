# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=meego-resource-git
pkgver=0.25.0.r0.gc9220d7
pkgrel=1
pkgdesc="MeeGo resource management low level C API libraries"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libresource"
license=('LGPL-2.1-only')
depends=('dbus-glib' 'check')
makedepends=('git' 'automake' 'autoconf' 'libtool')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${pkgname}"
    git submodule update --init --recursive
    echo "$pkgver" > .tarball-version
    ./autogen.sh --disable-static
}


build() {
    cd "${srcdir}/${pkgname}"
    ./configure --prefix=/usr --disable-static
    make -j1 #FIXME: For some reason it breaks otherwise
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="$pkgdir/" install
    # Remove tests
    rm -rf "$pkgdir"/opt

}
 
