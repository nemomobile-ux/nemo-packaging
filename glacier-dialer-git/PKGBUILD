# Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

_host="github.com"
_project=nemomobile-ux
_basename=glacier-dialer
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=0.4.r23.ga2d5228

pkgrel=1
pkgdesc="QML based dialer application for nemomobile"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname"
license=('BSD-3-Clause')
depends=('qt5-glacier-app-git' 'nemo-qml-plugin-contacts-git' 'voicecall' 'commhistory-daemon-git' 'libcommhistory-git' 'libqofono-qt5' 'nemo-qml-plugin-dbus')
makedepends=('git' 'cmake' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install

    mkdir -p ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants
    ln -s ../voicecall-ui-prestart.service ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants/

    mkdir -p ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    cd ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    ln -s /usr/share/glacier-dialer/images/glacier-dialer.png icon-lock-missed-call.png
}
